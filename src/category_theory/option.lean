/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import order.bounded_lattice
import category_theory.punit
import category_theory.limits.shapes.terminal

/-!
# Category structures on `option C`, `with_bot C`, and `with_top C`, along with basic constructions.

## Categories

We define

* a category structure on `option C`, so there are no morphisms to or from `none`,
  and the morphisms `some X ‚ü∂ some Y` are just the original morphisms `X ‚ü∂ Y`.

* a category structure on `with_bot C`, so `none` has a unique morphism to every object,
  and the morphisms `some X ‚ü∂ some Y` are just the original morphisms `X ‚ü∂ Y`.

* a category structure on `with_top C`, so `none` has a unique morphism from every object,
  and the morphisms `some X ‚ü∂ some Y` are just the original morphisms `X ‚ü∂ Y`.

## Limits

By construction, `with_bot C` has an initial object, and `with_top C` has a terminal object.
-/

universes v‚ÇÅ u‚ÇÅ

open category_theory.limits

namespace category_theory

variables (C : Type u‚ÇÅ) [ùíû : category.{v‚ÇÅ} C]
include ùíû

section
local attribute [tidy] tactic.case_bash

instance category_option : category.{v‚ÇÅ} (option C) :=
{ hom := Œª X Y, match X, Y with
  | some X, some Y := X ‚ü∂ Y
  | none, none := punit
  | _, _ := pempty
  end,
  id := Œª X, match X with
  | some X := ùüô X
  | none := punit.star
  end,
  comp := Œª X Y Z f g, match X, Y, Z, f, g with
  | some X, some Y, some Z, f, g := f ‚â´ g
  | none, none, none, _, _ := punit.star
  end }

instance category_with_bot : category.{v‚ÇÅ} (with_bot C) :=
{ hom := Œª X Y, match X, Y with
  | some X, some Y := X ‚ü∂ Y
  | none, _ := punit
  | _, _ := pempty
  end,
  id := Œª X, match X with
  | some X := ùüô X
  | none := punit.star
  end,
  comp := Œª X Y Z f g, match X, Y, Z, f, g with
  | some X, some Y, some Z, f, g := f ‚â´ g
  | none, _, _, _, _ := punit.star
  end }

instance category_with_top : category.{v‚ÇÅ} (with_top C) :=
{ hom := Œª X Y, match X, Y with
  | some X, some Y := X ‚ü∂ Y
  | some X, none := punit
  | none, some Y := pempty
  | none, none := punit
  end,
  id := Œª X, match X with
  | some X := ùüô X
  | none := punit.star
  end,
  comp := Œª X Y Z f g, match X, Y, Z, f, g with
  | some X, some Y, some Z, f, g := f ‚â´ g
  | some X, some Y, none, _, _ := punit.star
  | some X, none, none, _, _ := punit.star
  | none, none, none, _, _ := punit.star
  end }
end

namespace functor

def none : punit.{u‚ÇÅ+1} ‚•§ option C :=
{ obj := Œª X, none,
  map := Œª X Y f, ùüô _ }

instance full_none : full (none C) :=
{ preimage := Œª X Y f, by { cases X, cases Y, exact punit.star } }
instance faithful_none : faithful (none C) := {}

def some : C ‚•§ option C :=
{ obj := Œª X, some X,
  map := Œª X Y f, f }

instance full_some : full (some C) :=
{ preimage := Œª X Y f, f }
instance faithful_some : faithful (some C) := {}

section
local attribute [tidy] tactic.case_bash

def option_to_with_bot : option C ‚•§ with_bot C :=
{ obj := Œª X, X,
  map := Œª X Y f, match X, Y, f with
  | option.some X, option.some Y, f := f
  | option.none, option.none, f := punit.star
  end }

instance faithful_option_to_with_bot : faithful (option_to_with_bot C) := {}

def option_to_with_top : option C ‚•§ with_top C :=
{ obj := Œª X, X,
  map := Œª X Y f, match X, Y, f with
  | option.some X, option.some Y, f := f
  | option.none, option.none, f := punit.star
  end }

instance faithful_option_to_with_top : faithful (option_to_with_top C) := {}

end

end functor

end category_theory


namespace category_theory

variables (C : Type u‚ÇÅ) [ùíû : category.{v‚ÇÅ+1} C]
include ùíû

instance has_initial_with_bot : has_initial.{v‚ÇÅ} (with_bot C) :=
has_initial.of_unique none (Œª X, by tidy)

local attribute [tidy] tactic.case_bash

instance has_terminal_with_bot : has_terminal.{v‚ÇÅ} (with_top C) :=
has_terminal.of_unique none (Œª X, by tidy)

end category_theory

-- TODO prove the equivalence between `option C` and `C ‚äï punit`.
