-- Copyright (c) 2017 Scott Morrison. All rights reserved.
-- Released under Apache 2.0 license as described in the file LICENSE.
-- Authors: Patrick Massot, Scott Morrison, Mario Carneiro

import category_theory.concrete_category
import category_theory.full_subcategory
import category_theory.functor_category
import category_theory.adjunction
import category_theory.limits.types
import category_theory.natural_isomorphism
import category_theory.eq_to_hom
import topology.basic
import topology.opens
import order.galois_connection

open category_theory
open category_theory.nat_iso
open topological_space

universe u

namespace category_theory.instances

/-- The category of topological spaces and continuous maps. -/
@[reducible] def Top : Type (u+1) := bundled topological_space

instance (x : Top) : topological_space x := x.str

namespace Top
instance : concrete_category @continuous := ‚ü®@continuous_id, @continuous.comp‚ü©

-- local attribute [class] continuous
-- instance {R S : Top} (f : R ‚ü∂ S) : continuous (f : R ‚Üí S) := f.2

section
open category_theory.limits

variables {J : Type u} [small_category J]

def limit (F : J ‚•§ Top.{u}) : cone F :=
{ X := ‚ü®limit (F ‚ãô forget), ‚®Ü j, (F.obj j).str.induced (limit.œÄ (F ‚ãô forget) j)‚ü©,
  œÄ :=
  { app := Œª j, ‚ü®limit.œÄ (F ‚ãô forget) j, continuous_iff_induced_le.mpr (lattice.le_supr _ j)‚ü©,
    naturality' := Œª j j' f, subtype.eq ((limit.cone (F ‚ãô forget)).œÄ.naturality f) } }

def limit_is_limit (F : J ‚•§ Top.{u}) : is_limit (limit F) :=
by refine is_limit.of_faithful forget (limit.is_limit _) (Œª s, ‚ü®_, _‚ü©) (Œª s, rfl);
   exact continuous_iff_le_coinduced.mpr (lattice.supr_le $ Œª j,
     induced_le_iff_le_coinduced.mpr $ continuous_iff_le_coinduced.mp (s.œÄ.app j).property)

instance : has_limits.{u} Top.{u} :=
Œª J ùí• F, by exactI { cone := limit F, is_limit := limit_is_limit F }

instance : preserves_limits (forget : Top.{u} ‚•§ Type u) :=
Œª J ùí• F, by exactI preserves_limit_of_preserves_limit_cone
  (limit.is_limit F) (limit.is_limit (F ‚ãô forget))

def colimit (F : J ‚•§ Top.{u}) : cocone F :=
{ X := ‚ü®colimit (F ‚ãô forget), ‚®Ö j, (F.obj j).str.coinduced (colimit.Œπ (F ‚ãô forget) j)‚ü©,
  Œπ :=
  { app := Œª j, ‚ü®colimit.Œπ (F ‚ãô forget) j, continuous_iff_le_coinduced.mpr (lattice.infi_le _ j)‚ü©,
    naturality' := Œª j j' f, subtype.eq ((colimit.cocone (F ‚ãô forget)).Œπ.naturality f) } }

def colimit_is_colimit (F : J ‚•§ Top.{u}) : is_colimit (colimit F) :=
by refine is_colimit.of_faithful forget (colimit.is_colimit _) (Œª s, ‚ü®_, _‚ü©) (Œª s, rfl);
   exact continuous_iff_induced_le.mpr (lattice.le_infi $ Œª j,
     induced_le_iff_le_coinduced.mpr $ continuous_iff_le_coinduced.mp (s.Œπ.app j).property)

instance : has_colimits.{u} Top.{u} :=
Œª J ùí• F, by exactI { cocone := colimit F, is_colimit := colimit_is_colimit F }

instance : preserves_colimits (forget : Top.{u} ‚•§ Type u) :=
Œª J ùí• F, by exactI preserves_colimit_of_preserves_colimit_cocone
  (colimit.is_colimit F) (colimit.is_colimit (F ‚ãô forget))

end

def discrete : Type u ‚•§ Top.{u} :=
{ obj := Œª X, ‚ü®X, ‚ä§‚ü©,
  map := Œª X Y f, ‚ü®f, continuous_top‚ü© }

def trivial : Type u ‚•§ Top.{u} :=
{ obj := Œª X, ‚ü®X, ‚ä•‚ü©,
  map := Œª X Y f, ‚ü®f, continuous_bot‚ü© }

def adj‚ÇÅ : adjunction discrete forget :=
{ hom_equiv := Œª X Y,
  { to_fun := Œª f, f,
    inv_fun := Œª f, ‚ü®f, continuous_top‚ü©,
    left_inv := by tidy,
    right_inv := by tidy },
  unit := { app := Œª X, id },
  counit := { app := Œª X, ‚ü®id, continuous_top‚ü© } }

def adj‚ÇÇ : adjunction forget trivial :=
{ hom_equiv := Œª X Y,
  { to_fun := Œª f, ‚ü®f, continuous_bot‚ü©,
    inv_fun := Œª f, f,
    left_inv := by tidy,
    right_inv := by tidy },
  unit := { app := Œª X, ‚ü®id, continuous_bot‚ü© },
  counit := { app := Œª X, id } }

end Top

variables {X : Top.{u}}

-- To avoid mucky about with opposite categories, we just define the morphisms
-- in the direction we're always going to want to use.
instance : category.{u+1} (opens X) :=
{ hom  := Œª U V, ulift (plift (U ‚â• V)),
  id   := Œª X, ‚ü® ‚ü® le_refl X ‚ü© ‚ü©,
  comp := Œª X Y Z f g, ‚ü® ‚ü® ge_trans f.down.down g.down.down ‚ü© ‚ü© }
-- instance : category.{0} (opens X) :=
-- { hom  := Œª U V, U ‚â• V,
--   id   := Œª X, le_refl X,
--   comp := Œª X Y Z f g, ge_trans f g }

def nbhds (x : X.Œ±) := { U : opens X // x ‚àà U }
instance nbhds_category (x : X.Œ±) : category (nbhds x) := begin unfold nbhds, apply_instance end

def nbhds_inclusion (x : X.Œ±) : nbhds x ‚•§ opens X :=
{ obj := Œª U, U.val,
  map := Œª U V i, i }

end category_theory.instances

open category_theory.instances

namespace topological_space.opens

/-- `opens.map f` gives the functor from open sets in Y to open set in X,
    given by taking preimages under f. -/
def map
  {X Y : Top.{u}} (f : X ‚ü∂ Y) : opens Y ‚•§ opens X :=
{ obj := Œª U, ‚ü® f.val ‚Åª¬π' U, f.property _ U.property ‚ü©,
  map := Œª U V i, ‚ü® ‚ü® Œª a b, i.down.down b ‚ü© ‚ü© }.

@[simp] lemma map_id_obj {X : Top.{u}} (U : opens X) : (map (ùüô X)).obj U = U := by tidy

@[simp] def map_id (X : Top.{u}) : map (ùüô X) ‚âÖ functor.id (opens X) :=
{ hom := { app := Œª U, ùüô U },
  inv := { app := Œª U, ùüô U } }

@[simp] def map_comp {X Y Z : Top.{u}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : map (f ‚â´ g) ‚âÖ map g ‚ãô map f :=
{ hom := { app := Œª U, ùüô _ },
  inv := { app := Œª U, ùüô _ } }

-- We could make f g implicit here, but it's nice to be able to see when
-- they are the identity (often!)
def map_iso {X Y : Top.{u}} (f g : X ‚ü∂ Y) (h : f = g) : map f ‚âÖ map g :=
nat_iso.of_components (Œª U, eq_to_iso (congr_fun (congr_arg _ (congr_arg _ h)) _) ) (by obviously)

-- @[simp] lemma map_iso_id {X : Top.{u}} (h) : map_iso (ùüô X) (ùüô X) h = iso.refl (map _) := rfl
@[simp] lemma map_iso_refl {X Y : Top.{u}} (f : X ‚ü∂ Y) (h) : map_iso f f h = iso.refl (map _) := rfl

@[simp] lemma map_iso_hom_app {X Y : Top.{u}} (f g : X ‚ü∂ Y) (h : f = g) (U : opens Y) :
  (map_iso f g h).hom.app U = eq_to_hom (congr_fun (congr_arg functor.obj (congr_arg map h)) U) :=
rfl

@[simp] lemma map_iso_inv_app {X Y : Top.{u}} (f g : X ‚ü∂ Y) (h : f = g) (U : opens Y) :
  (map_iso f g h).inv.app U = eq_to_hom (congr_fun (congr_arg functor.obj (congr_arg map h.symm)) U) :=
rfl

end topological_space.opens

open topological_space

namespace topological_space.nbhds
variables {X Y : Top.{u}} (f : X ‚ü∂ Y)

def map (x : X) : nbhds (f x) ‚•§ nbhds x :=
{ obj := Œª U, ‚ü®(opens.map f).obj U.1, by tidy‚ü©,
  map := Œª U V i, (opens.map f).map i }

@[simp] lemma map_id_obj (x : X) (U : nbhds x) : (map (ùüô X) x).obj U = U := by tidy

def inclusion_map_iso (x : X) : nbhds_inclusion (f x) ‚ãô opens.map f ‚âÖ map f x ‚ãô nbhds_inclusion x :=
nat_iso.of_components
  (Œª U, begin split, exact ùüô _, exact ùüô _ end)
  (by tidy)

@[simp] lemma inclusion_map_iso_hom (x : X) : (inclusion_map_iso f x).hom = ùüô _ := rfl
@[simp] lemma inclusion_map_iso_inv (x : X) : (inclusion_map_iso f x).inv = ùüô _ := rfl

end topological_space.nbhds
