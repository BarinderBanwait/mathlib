/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, Scott Morrison
-/
import category_theory.enriched.enriched_over
import algebra.category.Module.basic
import linear_algebra.tensor_product

universes v u

open category_theory

section
variables (C : Type u) [ùíû : category.{v} C]
include ùíû

abbreviation preadditive := enriched_over.{v} AddCommGroup.{v} C
end

namespace AddCommGroup

instance : preadditive AddCommGroup :=
begin
  fconstructor,
  { exact Œª X Y, ‚ü®AddCommGroup.of (X ‚ü∂ Y), rfl‚ü©, },
  { intros X Y f Z,
    fconstructor,
    { fconstructor,
      { dsimp [bundled.map],
        exact Œª g, g.comp f, },
      { exact add_monoid_hom.zero_comp f, },
      { exact Œª x y, by { ext, dsimp, simp [add_monoid_hom.add_apply], }, }, },
    { refl, } },
  { intros X Y Z g,
    fconstructor,
    { fconstructor,
      { dsimp [bundled.map],
        exact Œª f, g.comp f, },
      { exact add_monoid_hom.comp_zero g, },
      { exact Œª x y, by { ext, dsimp, simp [add_monoid_hom.add_apply], }, }, },
    { refl, } },
end

-- This is gross because of
-- https://github.com/leanprover-community/lean/issues/197
-- If it weren't for that, we could replace the `erw` with `simp`.
instance fff : preadditive AddCommGroup :=
{ e_hom := Œª X Y, ‚ü®AddCommGroup.of (X ‚ü∂ Y), rfl‚ü©,
  e_comp_left := Œª X Y f Z,
  ‚ü®{ to_fun := Œª g, g.comp f,
     map_zero' := add_monoid_hom.zero_comp f,
     map_add' := Œª x y, by { ext, dsimp, erw [add_monoid_hom.add_apply], } },
  rfl‚ü©,
  e_comp_right := Œª X Y Z g,
  ‚ü®{ to_fun := Œª f, g.comp f,
     map_zero' := add_monoid_hom.comp_zero g,
     map_add' := Œª x y,
     begin
       ext,
       dsimp,
       erw [add_monoid_hom.add_apply],
       simp,
       erw [add_monoid_hom.add_apply],
       simp,
     end },
  rfl‚ü©, }.

end AddCommGroup

namespace Module

section
variables (R : Type u) [ring R]

instance : preadditive (Module R) :=
{ e_hom := Œª X Y, ‚ü®AddCommGroup.of (X ‚ü∂ Y), rfl‚ü©,
  e_comp_left := Œª X Y f Z,
  ‚ü®{ to_fun := Œª g, g.comp f, map_zero' := by simp, map_add' := Œª x y, by { ext, simp, } }, rfl‚ü©,
  e_comp_right := Œª X Y Z g,
  ‚ü®{ to_fun := Œª f, g.comp f, map_zero' := by simp, map_add' := Œª x y, by { ext, simp, } }, rfl‚ü©, }.
end

section
variables {R : Type} [ring R]
abbreviation Ab := AddCommGroup.{0}
variables {M N P : Module R}

-- We get an `AddCommGroup` worth of morphisms:
example : AddCommGroup := M ‚ü∂[Ab] N
-- We can add them!
example (f g : M ‚ü∂[Ab] N) : M ‚ü∂ N := f + g
-- We can see that composition is additive in either argument:
example (f : M ‚ü∂[Ab] N) : (N ‚ü∂[Ab] P) ‚Üí+ (M ‚ü∂[Ab] P) := comp_left Ab f P
-- Coercions to functions isn't as good as we'd like,
-- but we can verify that `comp_left` is definitionally what we expect:
example (f : M ‚ü∂[Ab] N) (g : N ‚ü∂[Ab] P) (m : M) :
  ((comp_left Ab f P : (N ‚ü∂[Ab] P) ‚Üí (M ‚ü∂[Ab] P)) g).to_fun m = g (f m) := rfl
end

section
variables (R : Type u) [comm_ring R]

instance : enriched_over (Module R) (Module R) :=
{ e_hom := Œª X Y, ‚ü®Module.of R (X ‚ü∂ Y), rfl‚ü©,
  e_comp_left := Œª X Y f Z, ‚ü®(linear_map.llcomp R X Y Z).flip f, rfl‚ü©,
  e_comp_right := Œª X Y Z g, ‚ü®linear_map.llcomp R X Y Z g, rfl‚ü©, }

-- Out of the box, we can treat morphisms between R-modules as elements of an R-module.
example (X Y : Module R) (r : R) (f g : X ‚ü∂[Module R] Y) : r ‚Ä¢ (f + g) = r ‚Ä¢ g + r ‚Ä¢ f :=
by simp [smul_add, add_comm]

-- Check that the coercion to functions works:
example (X Y : Module R) (f : X ‚ü∂[Module R] Y) : f (0 : X) = (0 : Y) := by simp

end

end Module
